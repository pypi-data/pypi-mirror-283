"""
    Copyright 2017 Inmanta

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

        http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    Contact: code@inmanta.com
"""
import exec

entity User extends std::ManagedResource, std::PurgeableResource:
    string name
    string group
    string[] groups=[]
    bool system=false
    string shell="/bin/bash"
    string homedir
end

User.host [1] -- std::Host

index User(host, name)

entity Group extends std::ManagedResource, std::PurgeableResource:
    string name
    bool system=false
end

Group.host [1] -- std::Host

index Group(host, name)

implementation execUser for User:
    """
        Exec based implementation until a handler is available
    """
    run = exec::Run(host=host, command=std::template("user/useradd.j2"), unless="grep -E '^{{name}}' /etc/passwd")
    run.requires = self.requires
    run.provides = self.provides
end

implement User using execUser

implementation execGroup for Group:
    """
        Exec based implementation until a handler is available
    """
    run = exec::Run(host=host, command=std::template("user/groupadd.j2"), unless="grep -E '^{{name}}' /etc/group")
    run.requires = self.requires
    run.provides = self.provides
end

implement Group using execGroup